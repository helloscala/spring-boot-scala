buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
        scalaVersion = '2.11.8'
        scalaLibVersion = '2.11'
        jacksonVersion = '2.8.2'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '2.14.1'
}

subprojects {
//    apply plugin: 'java'
    apply plugin: 'scala'
//    apply plugin: 'eclipse'
    apply plugin: 'spring-boot'

    repositories {
        mavenCentral()
        jcenter()
    }

    group = 'com.helloscala.springbootscala'
    version = '1.0.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:-options"
    }

    dependencies {
        //compile('org.springframework.boot:spring-boot-starter-data-elasticsearch')
        //compile('org.springframework.boot:spring-boot-starter-data-jpa')
        //compile('org.springframework.boot:spring-boot-starter-data-redis')
        //compile('org.springframework.boot:spring-boot-starter-jooq')
//        compile('org.springframework.boot:spring-boot-devtools')
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-mail')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile("org.scala-lang:scala-library:$scalaVersion")
        compile("org.scala-lang:scala-reflect:$scalaVersion")
        compile("org.scala-lang.modules:scala-java8-compat_$scalaLibVersion:0.7.0")
        compile("com.fasterxml.jackson.module:jackson-module-scala_$scalaLibVersion:$jacksonVersion")
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")
        compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion")
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }

    // 这里需要重新设置Scala和Java源码的搜索路径，把Java源码路径移动到Scala的搜索路径来。不然编译时会遇到Java代码找不到Scala代码符号问题
    sourceSets {
        main {
            scala {
                srcDirs = ['src/main/scala', 'src/main/java']
            }
            java {
                srcDirs = []
            }
        }
        test {
            scala {
                srcDirs = ['src/test/scala', 'src/test/java']
            }
            java {
                srcDirs = []
            }
        }
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.additionalParameters = ["-feature", "-language:implicitConversions", "-language:reflectiveCalls"]
        //scalaCompileOptions.useAnt = false
        scalaCompileOptions.with {
            force = true
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    //eclipse {
    //	classpath {
    //		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
    //		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    //	}
    //}

}

